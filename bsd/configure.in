AC_INIT(krlogind.c)
WITH_CCOPTS
ISODE_DEFS
ISODE_INCLUDE
CONFIG_RULES
AC_SET_BUILDTOP
AC_PROG_INSTALL
AC_CHECK_LIB(socket,main)
AC_CHECK_LIB(nsl,main)
dnl dbm libs for use of an_to_ln
AC_CHECK_LIB(ndbm,main)
AC_CHECK_LIB(dbm,main)
dnl
dnl AIX has them all; SCO might too
LOGINLIBS=
AC_CHECK_LIB(odm,main,
  AC_CHECK_LIB(s,main,
    AC_CHECK_LIB(cfg,main, 
      LOGINLIBS="-lodm -ls -lcfg" 
      )))
AC_SUBST(LOGINLIBS)
dnl
AC_VFORK
AC_TYPE_MODE_T
AC_FUNC_CHECK(strsave,AC_DEFINE(HAS_STRSAVE))
AC_FUNC_CHECK(utimes,AC_DEFINE(HAS_UTIMES))
AC_FUNC_CHECK(getutent,AC_DEFINE(HAVE_GETUTENT))
AC_FUNC_CHECK(setreuid,AC_DEFINE(HAVE_SETREUID))
AC_FUNC_CHECK(gettosbyname,AC_DEFINE(HAVE_GETTOSBYNAME))
AC_FUNC_CHECK(killpg,AC_DEFINE(HAVE_KILLPG))
AC_FUNC_CHECK(initgroups,AC_DEFINE(HAVE_INITGROUPS))
AC_FUNC_CHECK(setpriority,AC_DEFINE(HAVE_SETPRIORITY))
AC_FUNC_CHECK(setreuid,AC_DEFINE(HAVE_SETREUID))
AC_FUNC_CHECK(setresuid,AC_DEFINE(HAVE_SETRESUID))
AC_FUNC_CHECK(waitpid,AC_DEFINE(HAVE_WAITPID))
AC_FUNC_CHECK(setsid,AC_DEFINE(HAVE_SETSID))
AC_FUNC_CHECK(line_push,AC_DEFINE(HAVE_LINE_PUSH))
AC_FUNC_CHECK(ptsname,AC_DEFINE(HAVE_PTSNAME))
AC_HAVE_HEADERS(sys/filio.h sys/sockio.h unistd.h stdlib.h sys/label.h sys/tty.h ttyent.h lastlog.h sys/select.h sys/ptyvar.h)
AC_REPLACE_FUNCS(getdtablesize)
CHECK_SIGNALS
CHECK_SETJMP
CHECK_DIRENT
CHECK_FCNTL
CHECK_WAIT_TYPE
AC_HEADER_CHECK(termios.h,AC_FUNC_CHECK([tcsetattr],AC_DEFINE(POSIX_TERMIOS)))
KRB_INCLUDE
CHECK_UTMP
WITH_KRB5ROOT
WITH_KRB4
dnl
dnl Check for where the BSD rlogin, rcp, and rsh programs live.
dnl
save_path=$PATH
PATH=/bin:/usr/bin:/usr/bsd:/usr/ucb
AC_PATH_PROG(UCB_RLOGIN,rlogin,/usr/ucb/rlogin)
AC_PATH_PROG(UCB_RSH,rsh,/usr/ucb/rsh)
AC_PATH_PROG(UCB_RCP,rcp,/usr/ucb/rcp)
PATH=$save_path
dnl
AC_ARG_WITH([krb4],[include Kerberos V4 support],
ADD_DEF(-DKRB5_KRB4_COMPAT)
K4LIB='$(KRB4)/lib/libkrb.a $(KRB4)/lib/libdes.a',
echo "warning: no krb4 backwards compatibility support"
K4LIB=)
AC_SUBST(K4LIB)
dnl
dnl
AC_MSG_CHECKING([streams interface])
AC_TRY_COMPILE(
[#include <sys/stream.h>
#include <sys/stropts.h>], [],
[AC_MSG_RESULT(yes)
AC_DEFINE(HAVE_STREAMS)],
AC_MSG_RESULT(no))
dnl
dnl
AC_MSG_CHECKING([F_SETOWN])
AC_TRY_COMPILE(
[#include <sys/types.h>
#include <fcntl.h>], [1+F_SETOWN;],
[AC_MSG_RESULT(yes)
AC_DEFINE(HAVE_SETOWN)],
AC_MSG_RESULT(no))
dnl
dnl
AC_MSG_CHECKING([setenv])
AC_TRY_LINK(
[],[setenv("PATH","/bin",0);],
AC_MSG_RESULT(yes),
[AC_MSG_RESULT(no)
SETENVSRC=setenv.c
SETENVOBJ=setenv.o
AC_SUBST([SETENVSRC])
AC_SUBST([SETENVOBJ])])
dnl
dnl
AC_MSG_CHECKING([arguments to getpgrp])
AC_TRY_COMPILE(
[#include <unistd.h>], [int pid = getpgrp()],
[AC_MSG_RESULT(void)
AC_DEFINE(GETGRP_ONEARG)],
AC_MSG_RESULT(pid))
dnl
dnl
AC_MSG_CHECKING([number of arguments to setpgrp])
AC_TRY_COMPILE(
[#include <unistd.h>],[setpgrp(0,0)],
[AC_MSG_RESULT(two)
AC_DEFINE(SETPGRP_TWOARG)],
AC_MSG_RESULT(void))
dnl
dnl
ADD_DEF(-DKERBEROS)
V5_AC_OUTPUT_MAKEFILE
