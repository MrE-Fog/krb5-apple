AC_INIT(krlogind.c)
WITH_CCOPTS
ISODE_DEFS
ISODE_INCLUDE
CONFIG_RULES
AC_SET_BUILDTOP
AC_PROG_INSTALL
AC_CHECK_LIB(socket,main)
AC_CHECK_LIB(nsl,main)
dnl dbm libs for use of an_to_ln
AC_CHECK_LIB(ndbm,main)
AC_CHECK_LIB(dbm,main)
dnl
dnl AIX has them all; SCO might too
LOGINLIBS=
AC_CHECK_LIB(odm,main,
  AC_CHECK_LIB(s,main,
    AC_CHECK_LIB(cfg,main, 
      LOGINLIBS="-lodm -ls -lcfg" 
      )))
AC_SUBST(LOGINLIBS)
dnl
AC_VFORK
AC_FUNC_CHECK(strsave,AC_DEFINE(HAS_STRSAVE))
AC_FUNC_CHECK(utimes,AC_DEFINE(HAS_UTIMES))
AC_FUNC_CHECK(getutent,AC_DEFINE(HAVE_GETUTENT))
AC_FUNC_CHECK(setreuid,AC_DEFINE(HAVE_SETREUID))
AC_FUNC_CHECK(gettosbyname,AC_DEFINE(HAVE_GETTOSBYNAME))
AC_FUNC_CHECK(killpg,AC_DEFINE(HAVE_KILLPG))
AC_FUNC_CHECK(getdtablesize,AC_DEFINE(HAVE_GETDTABLESIZE))
AC_FUNC_CHECK(initgroups,AC_DEFINE(HAVE_INITGROUPS))
AC_FUNC_CHECK(setpriority,AC_DEFINE(HAVE_SETPRIORITY))
AC_FUNC_CHECK(setreuid,AC_DEFINE(HAVE_SETREUID))
AC_FUNC_CHECK(setresuid,AC_DEFINE(HAVE_SETRESUID))
AC_FUNC_CHECK(sigprocmask,
AC_COMPILE_CHECK([sigset_t],
[#include <signal.h>],
[sigset_t x],
AC_DEFINE(POSIX_SIGNALS)))
AC_HAVE_HEADERS(sys/filio.h sys/sockio.h unistd.h sys/label.h ttyent.h lastlog.h sys/select.h sys/ptyvar.h)
AC_COMPILE_CHECK([use streams interface],
[#include <sys/tty.h>
#include <sys/ttold.h>
#include <sys/ptyvar.h>],
[], AC_DEFINE(HAVE_STREAMS))
AC_REPLACE_FUNCS(getdtablesize)
CHECK_DIRENT
CHECK_FCNTL
CHECK_WAIT_TYPE
AC_WAIT3
AC_HEADER_CHECK(termios.h,AC_FUNC_CHECK([tcsetattr],AC_DEFINE(POSIX_TERMIOS)))
AC_COMPILE_CHECK([F_SETOWN],
[#include <sys/types.h>
#include <fcntl.h>],
[1+F_SETOWN;], 
AC_DEFINE(HAVE_SETOWN))
AC_COMPILE_CHECK([ut_host in struct utmp],
[#include <utmp.h>],
[struct utmp ut; ut.ut_host;],
,
AC_DEFINE(NO_UT_HOST))
AC_COMPILE_CHECK([ut_exit in struct utmp],
[#include <utmp.h>],
[struct utmp ut; ut.ut_exit;],
,
AC_DEFINE(NO_UT_EXIT))
AC_COMPILE_CHECK([ut_pid in struct utmp],
[#include <utmp.h>],
[struct utmp ut; ut.ut_pid;],
,
AC_DEFINE(NO_UT_PID))
KRB_INCLUDE
WITH_KRB5ROOT
WITH_KRB4
AC_ARG_WITH([krb4],[include Kerberos V4 support],
ADD_DEF(-DKRB5_KRB4_COMPAT)
K4LIB='$(KRB4)/lib/libkrb.a $(KRB4)/lib/libdes.a',
echo "warning: no krb4 backwards compatibility support"
K4LIB=)
AC_SUBST(K4LIB)
AC_COMPILE_CHECK([setenv],
[setenv();],[setenv("PATH","/bin",0)],,
[SETENVSRC=setenv.c
SETENVOBJ=setenv.o
AC_SUBST([SETENVSRC])
AC_SUBST([SETENVOBJ])])
dnl
AC_COMPILE_CHECK([number of arguments to getpgrp],
[#include <unistd.h>],[int pid = getpgrp()],
AC_DEFINE(GETGRP_ONEARG))
AC_COMPILE_CHECK([number of arguments to setpgrp],
[#include <unistd.h>],[setpgrp(0,0)],
AC_DEFINE(SETPGRP_TWOARG))
dnl
ADD_DEF(-DKERBEROS)
AC_HAVE_FUNCS(srand48 srand srandom)
V5_AC_OUTPUT_MAKEFILE
